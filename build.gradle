plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group 'page.nafuchoco'
version '2.2.0-dev.4'

sourceCompatibility = 11
targetCompatibility = 11
compileJava.options.encoding = 'UTF-8'

jar {
    manifest {
        attributes("Main-Class": "page.nafuchoco.neojukepro.api.NeoJukeBot",
                "Implementation-Title": "NeoJukePro",
                "Implementation-Version": getArchiveVersion().get()
        )
    }
}

shadowJar {
    archiveFileName.set("NeoJukePro." + archiveExtension.get())
}

task sourcesJar(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}
task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    archiveClassifier.set('javadoc')
}
javadoc {
    options.charSet = 'UTF-8'
    options.encoding = 'UTF-8'
}
task beforeDeploy {
    dependsOn('sourcesJar', 'javadocJar', 'shadowJar')
}
publishing {
    publications {
        mavenAar(MavenPublication) {
            from components.java
            afterEvaluate {
                artifact javadocJar
                artifact sourcesJar
            }
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
    maven { url 'https://m2.dv8tion.net/releases' }
}

dependencies {
    // Logger
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'ch.qos.logback:logback-core:1.2.6'
    implementation 'ch.qos.logback:logback-classic:1.2.6'
    implementation 'io.sentry:sentry-logback:5.2.3'
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    testCompileOnly 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

    // ClientLib
    implementation 'com.squareup.okhttp3:okhttp:4.9.2'
    api('net.dv8tion:JDA:4.3.0_339') {
        exclude module: 'opus-java'
    }
    api 'com.sedmelluq:lavaplayer:1.3.78'
    api 'com.github.freyacodes:Lavalink-Client:3ea4125'

    // Parser
    implementation 'com.google.code.gson:gson:2.8.8'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.0'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.0'

    // Commons Library
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    // SQL
    implementation 'com.zaxxer:HikariCP:5.0.0'
    implementation 'mysql:mysql-connector-java:8.0.26'
    implementation 'org.mariadb.jdbc:mariadb-java-client:2.7.4'

    // Other
    implementation 'de.vandermeer:asciitable:0.3.2'
}
