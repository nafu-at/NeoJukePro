plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group 'page.nafuchoco'
version '2.0.0-Beta4'

sourceCompatibility = 11
targetCompatibility = 11
compileJava.options.encoding = 'UTF-8'

jar {
    manifest {
        attributes("Main-Class": "page.nafuchoco.neojukepro.core.Main",
                "Implementation-Title": "NeoJukePro",
                "Implementation-Version": getArchiveVersion().get()
        )
    }
}

shadowJar {
    archiveFileName.set("NeoJukePro." + archiveExtension.get())
}

task sourcesJar(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}
task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    archiveClassifier.set('javadoc')
}
javadoc {
    options.charSet = 'UTF-8'
    options.encoding = 'UTF-8'
}
task beforeDeploy {
    dependsOn('sourcesJar', 'javadocJar', 'shadowJar')
}
publishing {
    publications {
        mavenAar(MavenPublication) {
            from components.java
            afterEvaluate {
                artifact javadocJar
                artifact sourcesJar
            }
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    // Logger
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'ch.qos.logback:logback-core:1.2.3'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'io.sentry:sentry-logback:4.0.0'
    compileOnly 'org.projectlombok:lombok:1.18.18'
    annotationProcessor 'org.projectlombok:lombok:1.18.14'
    testCompileOnly 'org.projectlombok:lombok:1.18.14'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.14'

    // ClientLib
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    api('net.dv8tion:JDA:4.2.0_212') {
        exclude module: 'opus-java'
    }
    api 'com.sedmelluq:lavaplayer:1.3.63'
    api 'com.github.FredBoat:Lavalink-Client:92a858d'

    // Parser
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.12.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.1'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.12.1'

    // Commons Library
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'org.apache.commons:commons-lang3:3.11'

    // SQL
    implementation 'com.zaxxer:HikariCP:4.0.1'
    implementation 'mysql:mysql-connector-java:8.0.23'
    implementation 'org.mariadb.jdbc:mariadb-java-client:2.7.2'

    // Other
    implementation 'de.vandermeer:asciitable:0.3.2'
}
